apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

Properties importStrings = new Properties()
importStrings.load(new FileInputStream(file(
        "../upr/src/com/dabomstew/pkrandom/newgui/Bundle.properties")))
def trimPrefix = ~/^GUI_/
def trimSuffix = ~/_?text$/
def prefixMapType = "java.util.HashMap<String, java.util.Map<String, String>>"
//group properties by prefix
Properties groupStrings = importStrings.groupBy {
    ((it.key.replaceAll("\\.", "_") - trimPrefix) - trimSuffix).split("[A-Z]", 2)[0]
}

android {
    compileSdkVersion 33
    buildToolsVersion "30.0.3"
    defaultConfig {
        applicationId "ly.mens.rndpkmn"
        minSdkVersion 29
        targetSdkVersion 33
        versionCode 20
        versionName "0.3.3"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        //add properties to resources
        importStrings.forEach { rawKey, value ->
            String key = (rawKey.replaceAll("\\.", "_") - trimPrefix) - trimSuffix
            resValue "string", key, "\"$value\""
        }
        //add properties to build config
        buildConfigField prefixMapType, "PREFIX_MAP", mapToString(groupStrings, prefixMapType)
        //add presets to build config
        //FRLG
        buildConfigField "String", "FRLG_STANDARD", '"321WQIEEjIBAAQABwCRAAKeBhsECQEACQACCQAuEgAAAAAABRi45ATkAYAICTIGBAIyAAUYEEZpcmUgUmVkIChVKSAxLjEDx+K248M4ig=="'
        buildConfigField "String", "FRLG_ULTIMATE", '"321WRIEEjIBAAQABwCRAAKeBhsECQEACQACCQAuEgAAAAAABRi45ATkAYAICTIGBAIyAAUYEEZpcmUgUmVkIChVKSAxLjG65Ip748M4ig=="'
        buildConfigField "String", "FRLG_KAIZO", '"321WRIEEjIBAAQABwCRAAKeBhsECQEAFAACCQAuEgAAAAAABRi45ATkAYYICTIGBAIyAAUYEEZpcmUgUmVkIChVKSAxLjF0u1o648M4ig=="'
        buildConfigField "String", "FRLG_SURVIVAL", '"321WRIEEjIBAAQABwCRAAKeBhsEiAEAFAACCQAuEgAAAAAABRi45ATkAYYICTIGBAIyEwUYEEZpcmUgUmVkIChVKSAxLjEV8TGN48M4ig=="'
        //RSE
        buildConfigField "String", "RSE_STANDARD", '"321WQIEEjL8AP8AAgGRAAKeBhsECQEACQACCQAuEgAAAAAABBC45ATkAYAICTIGBAIyAAUYC0VtZXJhbGQgKFUpZtO82OPDOIo="'
        buildConfigField "String", "RSE_ULTIMATE", '"321WRIEEjL8AP8AAgGRAAKeBhsECQEACQACCQAuEgAAAAAABBC45ATkAYAICTIGBAIyAAUYC0VtZXJhbGQgKFUp3LKaF+PDOIo="'
        buildConfigField "String", "RSE_KAIZO", '"321WRIEEjL8AP8AAgGRAAKeBhsECQEAFAACCQAuEgAAAAAABBC45ATkAYYICTIGBAIyAAUYC0VtZXJhbGQgKFUpY5koIePDOIo="'
        buildConfigField "String", "RSE_SURVIVAL", '"321WRIEEjL8AP8AAgGRAAKeBhsEiAEAFAACCQAuEgAAAAAABBC45ATkAYYICTIGBAIyEwUYC0VtZXJhbGQgKFUpgATpauPDOIo="'
        //HGSS
        buildConfigField "String", "HGSS_STANDARD", '"321WQIEEgKYAJsAngCRAAKeBhsECQEACQACCQAuEgAAAAAADBCI5ATkQYAICTIGBQIyAAUYFlBva2Vtb24gU291bFNpbHZlciAoVSnwKlW248M4ig=="'
        buildConfigField "String", "HGSS_ULTIMATE", '"321WRIEEgKYAJsAngCRAAKeBhsECQEACQACCQAuEgAAAAAADBCI5ATkQYAICTIGBQIyAAUYFlBva2Vtb24gU291bFNpbHZlciAoVSmMFqEf48M4ig=="'
        buildConfigField "String", "HGSS_KAIZO", '"321WRIEEgKYAJsAngCRAAKeBhsECQEAFAACCQAuEgAAAAAADBCI5ATkQYYICTIGBQIyAAUYFlBva2Vtb24gU291bFNpbHZlciAoVSnJuKMx48M4ig=="'
        buildConfigField "String", "HGSS_SURVIVAL", '"321WRIEEgKYAJsAngCRAAKeBhsEiAEAFAACCQAuEgAAAAAADBCI5ATkQYYICTIGBQIyEwUYFlBva2Vtb24gU291bFNpbHZlciAoVSmdKd/u48M4ig=="'
        //DPPT
        buildConfigField "String", "DPPT_STANDARD", '"321WQIEEjLDAXQA+ACRAAKeBhsECQEACQACCQAuEgAAAAAADBCI5ATkQYAICTIGBQIyAAUYGlBva2Vtb24gUGxhdGludW0gKFUgUmV2IDEp+Fri/OPDOIo="'
        buildConfigField "String", "DPPT_ULTIMATE", '"321WRIEEjLDAXQA+ACRAAKeBhsECQEACQACCQAuEgAAAAAADBCI5ATkQYAICTIGBQIyAAUYGlBva2Vtb24gUGxhdGludW0gKFUgUmV2IDEpULQrl+PDOIo="'
        buildConfigField "String", "DPPT_KAIZO", '"321WRIEEjLDAXQA+ACRAAKeBhsECQEAFAACCQAuEgAAAAAADBCI5ATkQYYICTIGBQIyAAUYGlBva2Vtb24gUGxhdGludW0gKFUgUmV2IDEpJe8DjOPDOIo="'
        buildConfigField "String", "DPPT_SURVIVAL", '"321WRIEEjLDAXQA+ACRAAKeBhsEiAEAFAACCQAuEgAAAAAADBCI5ATkQYYICTIGBQIyEwUYGlBva2Vtb24gUGxhdGludW0gKFUgUmV2IDEpAm8VwePDOIo="'
        //BW
        buildConfigField "String", "BW_STANDARD", '"321WQIEEgLvAfIB9QGRAAKeJBsECQEAFAACCQAuEgAAAAAABHCI5ATkwYAICTIGBgIyAAUYEVBva2Vtb24gQmxhY2sgKFUpIYfy/uPDOIo="'
        buildConfigField "String", "BW_ULTIMATE", '"321WRIEEgLvAfIB9QGRAAKeJBsECQEAFAACCQAuEgAAAAAABHCI5ATkwYAICTIGBgIyAAUYEVBva2Vtb24gQmxhY2sgKFUpxOtvm+PDOIo="'
        buildConfigField "String", "BW_KAIZO", '"321WRIEEgLvAfIB9QGRAAKeJBsECQEAFAACCQAuEgAAAAAABHCI5ATkwYYICTIGBgIyAAUYEVBva2Vtb24gQmxhY2sgKFUpVYq/AuPDOIo="'
        buildConfigField "String", "BW_SURVIVAL", '"321WRIEEgLvAfIB9QGRAAKeJBsEiAEAFAACCQAuEgAAAAAABHCI5ATkwYYICTIGBgIyEwUYEVBva2Vtb24gQmxhY2sgKFUpAO7TRuPDOIo="'
        //B2W2
        buildConfigField "String", "B2W2_STANDARD", '"321WQIEEgLvAfIB9QGRAAKeBhsECQEACQACCQAuEgAAAAAABHCI5ATkwYAICTIGBgAyAAUYE1Bva2Vtb24gQmxhY2sgMiAoVSkifvUE48M4ig=="'
        buildConfigField "String", "B2W2_ULTIMATE", '"321WRIEEgLvAfIB9QGRAAKeBhsECQEACQACCQAuEgAAAAAABHCI5ATkwYAICTIGBgIyAAUYE1Bva2Vtb24gQmxhY2sgMiAoVSkkLEYu48M4ig=="'
        buildConfigField "String", "B2W2_KAIZO", '"321WRIEEgLvAfIB9QGRAAKeBhsECQEACQACCQAuEgAAAAAABHCI5ATkwYYICTIGBgIyAAUYE1Bva2Vtb24gQmxhY2sgMiAoVSkCfDTI48M4ig=="'
        buildConfigField "String", "B2W2_SURVIVAL", '"321WRIEEgLvAfIB9QGRAAKeBhsEiAEACQACCQAuEgAAAAAABHCI5ATkwYYICTIGBgIyEwUYE1Bva2Vtb24gQmxhY2sgMiAoVSnSbcbe48M4ig=="'
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets.main.java.srcDirs = ['src/main/kotlin']
    namespace 'ly.mens.rndpkmn'
    buildFeatures {
        compose true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    composeOptions {
        kotlinCompilerVersion kotlin_version
        kotlinCompilerExtensionVersion "1.1.1"
    }
    lintOptions {
        checkReleaseBuilds false
    }
}

dependencies {
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'androidx.compose.ui:ui:1.3.3'
    implementation 'androidx.compose.ui:ui-tooling:1.3.3'
    implementation 'androidx.compose.foundation:foundation:1.3.1'
    implementation 'androidx.compose.material:material:1.3.1'
    implementation 'androidx.compose.material:material-icons-core:1.3.1'
    implementation 'androidx.compose.material:material-icons-extended:1.3.1'
    implementation 'androidx.activity:activity-compose:1.6.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.5.1'
    implementation 'androidx.compose.runtime:runtime-livedata:1.3.3'
    implementation 'androidx.navigation:navigation-compose:2.5.3'
    implementation 'androidx.documentfile:documentfile:1.0.1'
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4:1.1.1'
    testImplementation 'junit:junit:4.12'
    implementation project(':upr')
}
repositories {
    mavenCentral()
}

String mapToString(Map map, String type) {
    def prefix = "new $type(){{ "
    def suffix = '}}'
    def value = new StringBuilder()
    for (e in map) {
        if (e.value instanceof Map) {
            value.append(String.format('put("%s",%s); ', e.key, mapToString(e.value, "java.util.HashMap<String, String>")))
        } else {
            value.append(String.format('put("%s","%s"); ', e.key, e.value.replaceAll("\n", " ").replaceAll("\"", "'")))
        }
    }
    return String.format('%s%s%s', prefix, value, suffix)
}
